# Maintainer: Constantin Schomburg <me@xconstruct.net>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gtk2-overlay-scrollbar
pkgver=2.24.4
pkgrel=2
pkgdesc="The GTK+ Toolkit (v2) with overlay-scrollbar loading patch (Canonical Ayatana Project)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'heimdal'
         'gnutls' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'heimdal' 'gnutls'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
provides=("gtk2=${pkgver}")
conflicts=('gtk2')
options=('!libtool' '!docs')
install=gtk2.install
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.bz2
        xid-collision-debug.patch
		100_overlay_scrollbar_loading.patch)
sha256sums=('7d3033ad83647079977466d3e8f1a7533f47abd5cc693f01b8797ff43dd407a5'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
			'14e0a0e70970ba9dcff96b181dd1b813acd39433166733308462fc0e62659128')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
    patch -Np1 -i "${srcdir}/xid-collision-debug.patch"
	patch -Np1 -i "${srcdir}/100_overlay_scrollbar_loading.patch"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes
    make
}
package() {
    backup=(etc/gtk-2.0/gtkrc)

    cd "${srcdir}/gtk+-${pkgver}"

    make DESTDIR="${pkgdir}" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm ${pkgdir}/usr/bin/gtk-update-icon-cache
}
